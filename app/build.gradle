apply plugin: 'com.android.application'
apply plugin: 'android-apt'

def packageTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.duma.liudong.meiye"
        minSdkVersion 19
        targetSdkVersion 22
        versionCode 2
        versionName "1.02"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    signingConfigs {
        debug {

        }
        release {

        }
    }

    buildTypes {
        release {
            minifyEnabled true
            // Zipalign优化
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled true
            // Zipalign优化
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release  //使用上述签名信息
        }
    }

    //修改生成的最终文件名
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                File outputDirectory = new File(outputFile.parent);
                def fileName
                if (variant.buildType.name == "release") {
                    // 输出apk名称为app_v1.0.0_2015-06-15_playStore.apk
                    fileName = "app_v${defaultConfig.versionName}_${packageTime()}_release.apk"
                } else {
                    fileName = "app_v${defaultConfig.versionName}_${packageTime()}_beta.apk"
                }
                output.outputFile = new File(outputDirectory, fileName)
            }
        }
    }
    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }
}
repositories {
    flatDir {
        dirs 'libs' //this way we can find the .aar file in libs folder
    }
}

//读取key
File propFile = file('signing.properties');
if (propFile.exists()) {
    def Properties props = new Properties()
    props.load(new FileInputStream(propFile))
    if (props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
            props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
        android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
    } else {
        android.buildTypes.release.signingConfig = null
    }
} else {
    android.buildTypes.release.signingConfig = null
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support.constraint:constraint-layout:1.0.0-beta4'
    compile 'com.android.support:appcompat-v7:25.2.0'
    compile 'com.android.support:design:25.2.0'
    compile 'com.android.support:recyclerview-v7:25.2.0'
    compile 'com.android.support:support-vector-drawable:25.2.0'
    testCompile 'junit:junit:4.12'
    apt 'com.jakewharton:butterknife-compiler:8.4.0'
    //轮播图
    compile 'com.bigkoo:convenientbanner:2.0.5'
    //glide
    compile 'com.github.bumptech.glide:glide:3.7.0'
    //插件
    compile 'com.jakewharton:butterknife:8.4.0'
    //gson
    compile 'com.google.code.gson:gson:2.7'
    //RecyclerView的封装
    compile 'com.zhy:base-rvadapter:3.0.3'
    //okhttp的封装
    compile 'com.zhy:okhttputils:2.6.2'
    //eventbus
    compile 'org.greenrobot:eventbus:3.0.0'
    //PickerView
    compile 'com.contrarywind:Android-PickerView:3.2.4'
    compile 'top.zibin:Luban:1.0.9'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.1.6'
    compile 'com.zhy:flowlayout-lib:1.0.3'
    //tab悬浮
    compile 'com.github.w446108264:ScrollableLayout:1.0.3'
    //评价的星星
    compile 'com.hedgehog.ratingbar:app:1.1.2'
    //倒计时
    compile 'com.github.iwgang:countdownview:2.1.3'
    //银联
    compile files('libs/UPPayAssistEx.jar')
    compile files('libs/UPPayPluginExPro.jar')
    compile files('libs/alipaySdk-20161009.jar')
    //高德地图
    compile files('libs/AMap_Location_V3.3.0_20170118.jar')
    //微信支付
    compile 'com.tencent.mm.opensdk:wechat-sdk-android-without-mta:+'
    //工具类
    compile 'com.blankj:utilcode:1.3.7'
    //拼音
    compile 'com.belerweb:pinyin4j:2.5.1'
    //数据库
    compile files('libs/ormlite-android-5.0.jar')
    compile files('libs/ormlite-core-5.0.jar')
    //adapter
    compile 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.13'
    //未读消息aar
    compile(name: 'tipsview-release', ext: 'aar')
    //图片选择
    compile 'com.github.LuckSiege.PictureSelector:picture_library:v1.5.0'
    //日志
    compile 'com.orhanobut:logger:1.15'
}
